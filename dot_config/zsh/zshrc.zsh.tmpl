# Based on https://github.com/iloveitaly/dotfiles/tree/master

# =============
# PATH
# =============

# Homebrew
for brew_path in "/opt/homebrew/bin/brew" "/usr/local/bin/brew"; do
  if [[ -x "$brew_path" ]]; then
    eval "$($brew_path shellenv)"
    break
  fi
done
# See https://docs.brew.sh/Manpage#environment
export HOMEBREW_NO_ENV_HINTS=1
export HOMEBREW_NO_AUTO_UPDATE=1
export HOMEBREW_BUNDLE_FILE_GLOBAL="${XDG_CONFIG_HOME:-${HOME}/.config}/homebrew/Brewfile-machine"

# avoid installation via brew, this is not a supported installation method and breaks
# some directory structure assumptions that exist across the plugin ecosystem.
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
[ ! -d $ZINIT_HOME ] && mkdir -p "$(dirname $ZINIT_HOME)"
[ ! -d $ZINIT_HOME/.git ] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
source "${ZINIT_HOME}/zinit.zsh"

# unalias zi for zoxide later
unalias zi

# Golang
export GOROOT="$(brew --prefix golang)/libexec"
export GOPATH="$HOME/.local/go"
export PATH="$PATH:${GOPATH}/bin:${GOROOT}/bin"

# Rust
export PATH="$PATH:$HOME/.cargo/bin"

# Python
# if ! command -v python &> /dev/null
# then
#   export PATH="$PATH:$(brew --prefix python)/libexec/bin"
# fi

# Scripts
export PATH="$PATH:$HOME/.local/bin" # pipx and other things put stuff here
export PATH="$PATH:$HOME/.local/share/jackman/bin" # personal scripts
[ -d "$HOME/.dotfiles-local/bin" ] && export PATH="$PATH:$HOME/.dotfiles-local/bin"

# Application Bins
export PATH="$PATH:$HOME/.config/emacs/bin" # doom emacs
{{ if eq .chezmoi.os "darwin" -}}
[ -d "/Applications/Antigravity.app/" ] && export PATH="$PATH:$HOME/.antigravity/antigravity/bin"
{{ end }}

# Mise for pinned binaries, direnv, and tasks
zinit as="command" lucid from="gh-r" for \
    id-as="usage" \
    atpull="%atclone" \
    jdx/usage

zinit as="command" lucid from="gh-r" for \
    id-as="mise" mv="mise* -> mise" \
    atclone="./mise* completion zsh > _mise" \
    atpull="%atclone" \
    atload='eval "$(mise activate zsh)"' \
    jdx/mise

# =============
# ENV Vairables & App Settings
# =============
{{ $env_exports := (onepasswordItemFields "env" "Private" "jackman.1password.com") -}}
{{range $value := $env_exports }}export {{ $value.label }}='{{ $value.value }}'
{{end}}

export HOMEBREW_GITHUB_API_TOKEN='{{ onepasswordRead "op://Private/Github/homebrew_pat" "jackman.1password.com" }}'

export SPACECTL_SKIP_STACK_PROMPT='true'

export NOTI_DEFAULT="slack"

# =============
# Agent early return
# =============

# Skip zsh config for agent mode
if [[ "$PAGER" == "head -n 10000 | cat" || "$COMPOSER_NO_INTERACTION" == "1" ]]; then
  return
fi

# =============
# Completion
# =============

# forces zsh to realize new commands
zstyle ':completion:*' completer _oldlist _expand _complete _match _ignored _approximate

# matches case insensitive for lowercase
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

# pasting with tabs doesn't perform completion
zstyle ':completion:*' insert-tab pending

# rehash if command not found (possibly recently installed)
zstyle ':completion:*' rehash true

# speed https://coderwall.com/p/9fksra/speed-up-your-zsh-completions
zstyle ':completion:*' accept-exact '*(N)'
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.zsh/cache

# force completion generation for more obscure commands
zstyle :plugin:zsh-completion-generator   programs ncdu tre

# =============
# fzf-tab config
# =============

# set descriptions format to enable group support
zstyle ':completion:*:descriptions' format '[%d]'

# set list-colors to enable filename colorizing
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}

# preview directory's content with exa when completing cd
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'exa -1 --color=always $realpath'

# switch group using `,` and `.`
zstyle ':fzf-tab:*' switch-group ',' '.'

# menu if nb items > 2
zstyle ':completion:*' menu select=2

# preview directory's content with exa when completing cd
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always $realpath'

# don't show fzf unless there are more than 4 items
# zstyle ':fzf-tab:*' ignore false 4

# =============
# Shell Options
# man: zshoptions
# =============

setopt interactive_comments
setopt prompt_subst
setopt extended_glob            # Allow extended matchers like ^file, etc
setopt long_list_jobs
setopt auto_cd
setopt menu_complete            # Auto pick a menu match

# Set history behavior
# setopt share_history            # Share history across session
# setopt inc_append_history       # Dont overwrite history, add new entries immediately
setopt nosharehistory
setopt noincappendhistory
setopt extended_history         # Also record time and duration of commands.
setopt hist_expire_dups_first   # Clear duplicates when trimming internal hist.
setopt hist_find_no_dups        # Dont display duplicates during searches.
setopt hist_ignore_dups         # Ignore consecutive duplicates.
setopt hist_ignore_space        # Ignore commands prefixed with space
setopt hist_reduce_blanks       # Remove superfluous blanks.
setopt hist_save_no_dups        # Omit older commands in favor of newer ones.

# =============
# Interactive Sesttings
# =============
# without this some CLI tools (gh cli, for instance) do not page contents
export PAGER=less

# Don’t clear the screen after quitting a manual page
export MANPAGER="less -FX"

# more powerful less config
# https://www.topbug.net/blog/2016/09/27/make-gnu-less-more-powerful/
export LESS='--quit-if-one-screen --ignore-case --status-column --LONG-PROMPT --RAW-CONTROL-CHARS --HILITE-UNREAD --tabs=4 --window=-4'

if [[ "$TERM_PROGRAM" == "vscode" || "$TERM_PROGRAM" == "cursor" ]]; then
  export VISUAL=cursor
else
  export VISUAL=nvim
fi
export EDITOR="$VISUAL"

# =============
# Functions
# =============

# Worktrunk
if command -v wt >/dev/null 2>&1 || [[ -n "${WORKTRUNK_BIN:-}" ]]; then
  eval "$(wt config shell init zsh)"
fi

# TODO https://github.com/zdharma/zinit/issues/173#issuecomment-537325714
# ~/.config/zsh/zshrc_local.zsh can be used for settings you don’t want to commit
for file in aliases zshrc_local aliases_local; do
  local file="${XDG_CONFIG_HOME:-$HOME/.config}/zsh/$file.zsh"
  [ -e "$file" ] && source "$file"
done

# Autoload personal functions
function load_funcs() {
    # via https://stackoverflow.com/a/63661686
    local funcs="$1"

    if [[ -d $funcs ]]; then
        # add our local functions dir to the fpath
        # FPATH is already tied to fpath, but this adds
        # a uniqueness constraint to prevent duplicate entries
        typeset -TUg +x FPATH=$funcs:$FPATH fpath

        local names=(${=$(ls -1 "$funcs" | sed -E "/^_/d")})

        # Now autoload them
        if [ "${#names[@]}" -gt 0 ]; then
            autoload $names
        fi
    fi
}
load_funcs "${XDG_CONFIG_HOME:-${HOME}/.config}/zsh/functions"
load_funcs "${XDG_CONFIG_HOME:-${HOME}/.config}/zsh/functions_local"
unset -f load_funcs

# Place any completions here
COMPLETIONS_DIR="${XDG_CONFIG_HOME:-${HOME}/.config}/zsh/completions"
fpath+="${COMPLETIONS_DIR}"

# =============
# Plugins
# =============

source "${XDG_CONFIG_HOME:-$HOME/.config}/zsh/zsh_plugins.zsh"

# ===========
# Keybindings
# ===========
# open up current command in EDITOR, ctrl+x then ctrl+e
autoload -U edit-command-line
zle -N edit-command-line
bindkey '^X^E' edit-command-line

# save current command in buffer & restore after next command is run
# https://unix.stackexchange.com/a/74381
autoload -U push-input
# not sure why, but ^S is not getting passed to the terminal
bindkey '^X^P' push-input

# ===========
# Misc Config
# ===========

# Alias Tips setup
export ZSH_PLUGINS_ALIAS_TIPS_EXCLUDES="g e"
export ZSH_PLUGINS_ALIAS_TIPS_REVEAL=0
export ZSH_PLUGINS_ALIAS_TIPS_REVEAL_TEXT="Long version: "

# zsh-autosuggestions via atuin
ZSH_AUTOSUGGEST_STRATEGY=("atuin")

# https://til.hashrocket.com/posts/7evpdebn8g-remove-duplicates-in-zsh-path
typeset -U path

# # https://github.com/ohmyzsh/ohmyzsh/blob/master/lib/misc.zsh
# autoload -Uz url-quote-magic
# zle -N self-insert url-quote-magic

# =======================================
# navi
# =======================================
eval "$(navi widget zsh)"

# =======================================
# atuin shell history
# =======================================
eval "$(atuin init zsh)"

# Workaround to make vi-mode work with atuin
function zvm_after_init() {
  zvm_bindkey viins '^R' atuin-search
}

# =======================================
# zsh-autosuggest & bracketed-paste-magic
# =======================================
# DISABLE_MAGIC_FUNCTIONS=true

# ===========
# Word Definition
# ===========
# http://mikebian.co/fixing-word-navigation-in-zsh/
WORDCHARS='*?_-.[]~=&;!#$%^(){}<>/ '$'\n'
autoload -Uz select-word-style
select-word-style normal
zstyle ':zle:*' word-style unspecified
