# -*- mode: zsh -*- vim: ft=zsh

__gr_init() {
  setopt localoptions errexit

  local usage="Usage: ,gr-init [project-dir] [default-branch]

Initialize a new bare worktree git repository.

Arguments:
  project-dir     Directory to create for the project (prompted if omitted)
  default-branch  Branch name (defaults to init.defaultBranch or 'main')"

  if [[ "$1" == "-h" || "$1" == "--help" ]]; then
    echo "$usage"
    return 0
  fi

  local PROJECT_DIR=${1}
  if [[ -z "$PROJECT_DIR" ]]; then
    if command -v gum &> /dev/null; then
      PROJECT_DIR=$(gum input --placeholder "Project name")
    else
      read -r "PROJECT_DIR?Project directory: "
    fi
  fi

  if [[ -z "$PROJECT_DIR" ]]; then
    echo "Error: Project directory required"
    return 1
  fi

  local DEFAULT_BRANCH=${2:-$(git config --get init.defaultBranch || echo "main")}

  echo "Creating bare repository in ${PROJECT_DIR}/.bare"

  if ! mkdir -p "$PROJECT_DIR"; then
    echo "Error: Failed to create directory $PROJECT_DIR"
    return 1
  fi

  if ! pushd "$PROJECT_DIR" > /dev/null; then
    echo "Error: Failed to enter $PROJECT_DIR"
    return 1
  fi

  # Use mise to configure worktrunk paths
  mkdir -p .config
  cat >.config/mise.local.toml <<EOF
[env]
WORKTRUNK_WORKTREE_PATH = "{% raw %}../{{ branch | sanitize }}{% endraw %}"
EOF
  mise trust .config/mise.local.toml

  # Initialize bare repository
  if ! git init --bare .bare; then
    popd > /dev/null
    echo "Error: Failed to initialize bare repository"
    return 1
  fi
  echo "gitdir: ./.bare" > .git

  # Setup default branch worktree with initial commit
  if ! git worktree add --orphan "$DEFAULT_BRANCH"; then
    popd > /dev/null
    echo "Error: Failed to create worktree for $DEFAULT_BRANCH"
    return 1
  fi

  pushd "$DEFAULT_BRANCH"
  git commit --allow-empty -m "chore: initial commit"
  popd

  popd > /dev/null
  cd "$PROJECT_DIR/$DEFAULT_BRANCH"

  echo "Created bare repository with initial commit in $PROJECT_DIR/$DEFAULT_BRANCH"
}

if ! __gr_init "$@"; then
  return 1
fi
