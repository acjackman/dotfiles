# -*- mode: zsh -*- vim: ft=zsh

# _jgw_relativize_path - Convert absolute worktree path to relative display path
#
# Usage: _jgw_relativize_path <worktree_path> <main_repo_root>
# Returns: relative path for display

local wt_path="$1"
local main_repo_root="$2"

# Try to make path relative to main repo root
local rel_path="${wt_path#$main_repo_root/}"
# If path changed, it's under main repo root
if [[ "$rel_path" != "$wt_path" ]]; then
    echo "$rel_path"
    return 0
fi

# Not under main repo root, try to compute relative path with ..
if command -v grealpath >/dev/null 2>&1; then
    rel_path=$(grealpath --relative-to="$main_repo_root" "$wt_path" 2>/dev/null)
    if [[ $? -eq 0 && "$rel_path" != /* ]]; then
        echo "$rel_path"
        return 0
    fi
elif command -v realpath >/dev/null 2>&1; then
    rel_path=$(realpath --relative-to="$main_repo_root" "$wt_path" 2>/dev/null)
    if [[ $? -eq 0 && "$rel_path" != /* ]]; then
        echo "$rel_path"
        return 0
    fi
fi

# If realpath didn't work or returned absolute path, try $HOME/dev/ prefix
local dev_path="$HOME/dev/"
rel_path="${wt_path#$dev_path}"
if [[ "$rel_path" != "$wt_path" ]]; then
    echo "~/dev/$rel_path"
    return 0
fi

# fallback to absolute path
echo "$wt_path"
